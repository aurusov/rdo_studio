$Pattern образец_начала_обработки_детали_на_станке: rule trace
$Parameters
	номер_детали    : integer
$Relevant_resources
	_деталь: детали Keep
	_станок: станки Keep
$Body
_деталь
	Choice from _деталь.состояние = свободна and _деталь.номер = номер_детали
	Convert_rule
		состояние set занята
_станок
	Choice from _станок.номер = _деталь.обработано + 1 and _станок.состояние = свободен
	Convert_rule
		состояние set занят
		время_начала_обработки set система.time
		время_конца_обработки  set система.time + время(_станок.номер, _деталь.номер)
$End

$Pattern образец_окончания_обработки_детали_на_станке: rule trace
$Parameters
	номер_детали    : integer
$Relevant_resources
	_деталь : детали  Keep
	_станок : станки  Keep
$Body
_деталь
	Choice from _деталь.состояние = занята and _деталь.номер = номер_детали
	Convert_rule
		состояние set свободна
		обработано set _деталь.обработано + 1
_станок
	Choice from _станок.номер = _деталь.обработано + 1 and _станок.состояние = занят and _станок.время_конца_обработки <= система.time
	Convert_rule
		состояние set свободен
		время_начала_обработки set 0
		время_конца_обработки  set 1000
$End

$Pattern образец_перевода_времени: rule trace
$Relevant_resources
	_система: система Keep
$Body
_система
	Choice from ( станок_1.состояние = занят and станок_2.состояние = занят ) 
			or ( станок_1.состояние = занят and for_all(детали: детали.обработано = 0) )
			or ( станок_2.состояние = занят and for_all(детали: детали.обработано > 0) )
			or ( станок_1.состояние = занят and станок_2.состояние = свободен and for_all(детали: детали.обработано <> 1) )
//			or ( станок_1.состояние = свободен and станок_2.состояние = занят and for_all(детали: детали.обработано > 0) )
	Convert_rule
		time set min(станок_1.время_конца_обработки, станок_2.время_конца_обработки)
$End

$Pattern образец_перевода_времени_ie: irregular_event trace
$Relevant_resources
	_система: система Keep
$Time = 1
$Body
_система
	convert_event time set _система.time + 1
$End
