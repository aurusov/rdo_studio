$Pattern Образец_Прибытие_Заготовок : irregular_event trace
$Relevant_resources
	Новая_Заготовка		:Детали 	Create
	Входной_Накопитель	:ВхНак	Keep
$Time = Интервал_прихода_заготовок(150)
$Body
Новая_Заготовка
	Convert_event trace
		Положение 	 set ВхНак
		Время		 set Time_now
		
Входной_Накопитель	
	Convert_event 
		Длина_очереди set Входной_Накопитель.Длина_очереди + 1
$End

$Pattern Образец_Работы_Робота : operation
$Relevant_resources
	Робот_		: Ресурсы	 keep 		keep
	Деталь		: Детали	 keep	keep
	Накопитель1	: Накопители keep 		NoChange
	Накопитель2	: Накопители NoChange 	keep
$Time = Время_захвата(8,1) + Время_на_участке(Накопитель1.Положение) + Время_захвата(8,1)
$Body
Робот_	
	Choice from Робот_.Индекс = 5 and Робот_.Состояние = Свободен
	Convert_begin
		Положение set Новое_положение(Деталь.Положение)
		Состояние set Занят
	Convert_end
		Состояние set Новое_Состояние_Ресурса(Робот_.Состояние)
Деталь
	Choice from Деталь.Положение = ВхНак or Деталь.Положение = ВыхНакСт1 
	or Деталь.Положение = ВыхНакСт2
	with_min	Деталь.Время
	Convert_begin
		Положение set Робот_.Положение
	Convert_end 
		Положение set Накопитель_Завершение(Деталь.Положение, Робот_.Состояние)
		Время	  set Time_now
Накопитель1
	Choice from Накопитель1.Положение = Деталь.Положение and Накопитель1.Длина_очереди > 0
	Convert_begin
		Длина_очереди set ИзОчередь_Завершение(Накопитель1.Длина_очереди, Робот_.Состояние)

Накопитель2
	Choice from Накопитель2.Положение = Новый_накопитель(Накопитель1.Положение)
	Convert_end
		Длина_очереди set ВОчередь_Завершение(Накопитель2.Длина_очереди, Робот_.Состояние)
$End

$Pattern Образец_Работы_Ст1 : operation
$Relevant_resources
	Станок		: Ресурсы	 keep 		keep
	Накопитель1	: Накопители keep 		NoChange
	Накопитель2	: Накопители NoChange 	keep
	Деталь		: Детали	 keep 		keep
$Time = Обработка_на_станке_1(60,10)
$Body
Станок	
	Choice from Станок.Состояние = Свободен and (Станок.Индекс = 1 or Станок.Индекс = 2 
	or Станок.Индекс = 3)
	Convert_begin
		Состояние set Занят
	Convert_end
		Состояние set Новое_Состояние_Ресурса(Станок.Состояние)

Накопитель1
	Choice from Накопитель1.Положение = ВхНакСт1 and Накопитель1.Длина_очереди > 0
	Convert_begin
		Длина_очереди set ИзОчередь_Завершение(Накопитель1.Длина_очереди, Станок.Состояние)
	
Накопитель2
	Choice from Накопитель2.Положение = ВыхНакСт1 
	Convert_end
		Длина_очереди set ВОчередь_Завершение(Накопитель2.Длина_очереди, Станок.Состояние)
		
Деталь
	Choice from Деталь.Положение = ВхНакСт1 
	with_min	Деталь.Время
	Convert_begin
		Положение set Станок.Положение
	Convert_end 
		Положение set Накопитель_Завершение(Деталь.Положение, Станок.Состояние)
		Время	  set Time_now
$End

$Pattern Образец_Работы_Ст2 : operation
$Relevant_resources
	Станок		: Ресурсы	 keep 		keep
	Накопитель1	: Накопители keep 		NoChange
	Накопитель2	: Накопители NoChange 	keep
	Деталь		: Детали	 keep 		keep
$Time = Обработка_на_станке_2(100)
$Body
Станок	
	Choice from Станок.Состояние = Свободен and Станок.Индекс = 4
	Convert_begin
		Состояние set Занят
	Convert_end
		Состояние set Новое_Состояние_Ресурса(Станок.Состояние)

Накопитель1
	Choice from Накопитель1.Положение = ВхНакСт2
	Convert_begin
		Длина_очереди set ИзОчередь_Завершение(Накопитель1.Длина_очереди, Станок.Состояние)
Накопитель2
	Choice from Накопитель2.Положение = ВыхНакСт2
	Convert_end
		Длина_очереди set ВОчередь_Завершение(Накопитель2.Длина_очереди, Станок.Состояние)
		
Деталь
	Choice from Деталь.Положение = ВхНакСт2 
	with_min	Деталь.Время
	Convert_begin
		Положение set Станок.Положение
	Convert_end 
		Положение set Накопитель_Завершение(Деталь.Положение, Станок.Состояние)
		Время	  set Time_now
$End

$Pattern  Образец_Поломка_Ресурса : keyboard
$Parameters
	Индекс: integer
$Relevant_resources
	Ресурс : Ресурсы Keep NoChange
$Time = 0.0
$Body
Ресурс
	Choice from Ресурс.Индекс = Индекс
	Convert_begin
		Состояние set новое_состояние_ресурса(Ресурс.Состояние)
$End

